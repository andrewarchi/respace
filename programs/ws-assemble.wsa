# Press ^@ (Ctrl+@) to insert NUL and exit the interpreter

.parse_loop:
    call parse
    jmp .parse_loop

parse:
    call read_char

    # case [Space]: Stack Manipulation
    dup
    push ' '
    sub
    jz parse_stack

    # case [Tab]: Arithmetic, Heap Access, or I/O
    dup
    push '\t'
    sub
    jz .parse_tab

    # case [LF]: Flow Control
    dup
    push '\n'
    sub
    jz parse_flow

    # default: ignore character
    jmp parse

.parse_tab:
    call read_char

    # case [Tab][Space]: Arithmetic
    dup
    push ' '
    sub
    jz parse_arith

    # case [Tab][Tab]: Heap Access
    dup
    push '\t'
    sub
    jz parse_heap

    # case [Tab][LF]: I/O
    dup
    push '\n'
    sub
    jz parse_io

    # default: ignore character
    jmp .parse_tab

parse_stack:
    call read_char

    # case [Space]: push
    dup
    push ' '
    sub
    jz assemble_push

    # case [Tab]: copy, slide, or invalid instruction
    dup
    push '\t'
    sub
    jz .parse_stack_tab

    # case [LF]: dup, swap, or drop
    dup
    push '\n'
    sub
    jz .parse_stack_lf

    # default: ignore character
    jmp parse_stack

.parse_stack_tab:
    call read_char

    # case [Tab][Space]: copy
    dup
    push ' '
    sub
    jz assemble_copy

    # case [Tab][Tab]: invalid instruction
    dup
    push '\t'
    sub
    jz invalid_instr

    # case [Tab][LF]: slide
    dup
    push '\n'
    sub
    jz assemble_slide

    # default: ignore character
    jmp .parse_stack_tab

.parse_stack_lf:
    call read_char

    # case [LF][Space]: dup
    dup
    push ' '
    sub
    jz assemble_dup

    # case [LF][Tab]: swap
    dup
    push '\t'
    sub
    jz assemble_swap

    # case [LF][LF]: drop
    dup
    push '\n'
    sub
    jz assemble_drop

    # default: ignore character
    jmp .parse_stack_lf

parse_arith:
    call read_char

    # case [Space]: add, sub, or mul
    dup
    push ' '
    sub
    jz .parse_arith_space

    # case [Tab]: div, mod, or invalid
    dup
    push '\t'
    sub
    jz .parse_arith_tab

    # case [LF]: invalid instruction
    dup
    push '\n'
    sub
    jz .invalid_instr

    # default: ignore character
    jmp parse_arith

.parse_arith_space:
    call read_char

    # case [Space][Space]: add
    dup
    push ' '
    sub
    jz assemble_add

    # case [Space][Tab]: sub
    dup
    push '\t'
    sub
    jz assemble_sub

    # case [Space][LF]: mul
    dup
    push '\n'
    sub
    jz assemble_mul

    # default: ignore character
    jmp .parse_arith_space

.parse_arith_tab:
    call read_char

    # case [Tab][Space]: div
    dup
    push ' '
    sub
    jz assemble_div

    # case [Tab][Tab]: mod
    dup
    push '\t'
    sub
    jz assemble_mod

    # case [Tab][LF]: invalid instruction
    dup
    push '\n'
    sub
    jz .invalid_instr

    # default: ignore character
    jmp .parse_arith_tab

parse_heap:
    call read_char

    # case [Space]: store
    dup
    push ' '
    sub
    jz assemble_store

    # case [Tab]: retrieve
    dup
    push '\t'
    sub
    jz assemble_retrieve

    # case [LF]: invalid instruction
    dup
    push '\n'
    sub
    jz invalid_instr

    # default: ignore character
    jmp parse_heap

parse_flow:
    call read_char

    # case [Space]: label, call, or jump
    dup
    push ' '
    sub
    jz .parse_flow_space

    # case [Tab]: jz, jn, or ret
    dup
    push '\t'
    sub
    jz .parse_flow_tab

    # case [LF]: end or invalid instruction
    dup
    push '\n'
    sub
    jz .parse_flow_lf

    # default: ignore character
    jmp parse_flow

.parse_flow_space:
    call read_char

    # case [Space][Space]: label
    dup
    push ' '
    sub
    jz assemble_label

    # case [Space][Tab]: call
    dup
    push '\t'
    sub
    jz assemble_call

    # case [Space][LF]: jmp
    dup
    push '\n'
    sub
    jz assemble_jmp

    # default: ignore character
    jmp .parse_flow_space

.parse_flow_tab:
    call read_char

    # case [Tab][Space]: jz
    dup
    push ' '
    sub
    jz assemble_jz

    # case [Tab][Tab]: jn
    dup
    push '\t'
    sub
    jz assemble_jn

    # case [Tab][LF]: ret
    dup
    push '\n'
    sub
    jz assemble_ret

    # default: ignore character
    jmp .parse_flow_tab

.parse_flow_lf:
    call read_char

    # case [LF][Space]: invalid instruction
    dup
    push ' '
    sub
    jz invalid_instr

    # case [LF][Tab]: invalid instruction
    dup
    push '\t'
    sub
    jz invalid_instr

    # case [LF][LF]: end
    dup
    push '\n'
    sub
    jz assemble_end

    # default: ignore character
    jmp .parse_flow_lf

parse_io:
    call read_char

    # case [Space]: printc, printi, or invalid
    dup
    push ' '
    sub
    jz .parse_io_space

    # case [Tab]: readc, readi, or invalid
    dup
    push '\t'
    sub
    jz .parse_io_tab:

    # case [LF]: invalid instruction
    dup
    push '\n'
    sub
    jz invalid_instr

    # default: ignore character
    jmp parse_io

.parse_io_space:
    call read_char

    # case [Space][Space]: printc
    dup
    push ' '
    sub
    jz assemble_printc

    # case [Space][Tab]: printi
    dup
    push '\t'
    sub
    jz assemble_printi

    # case [Space][LF]: invalid instruction
    dup
    push '\n'
    sub
    jz invalid_instr

    # default: ignore character
    jmp .parse_io_space

.parse_io_tab:
    call read_char

    # case [Tab][Space]: readc
    dup
    push ' '
    sub
    jz assemble_readc

    # case [Tab][Tab]: readi
    dup
    push '\t'
    sub
    jz assemble_readi

    # case [Tab][LF]: invalid instruction
    dup
    push '\n'
    sub
    jz invalid_instr

    # default: ignore character
    jmp .parse_io_tab

assemble_push:
    push "push"
    jmp print_instr_integer
assemble_dup:
    push "dup"
    jmp print_instr
assemble_copy:
    push "copy"
    jmp print_instr_integer
assemble_swap:
    push "swap"
    jmp print_instr
assemble_drop:
    push "drop"
    jmp print_instr
assemble_slide:
    push "slide"
    jmp print_instr_integer

assemble_add:
    push "add"
    jmp print_instr
assemble_sub:
    push "sub"
    jmp print_instr
assemble_mul:
    push "mul"
    jmp print_instr
assemble_div:
    push "div"
    jmp print_instr
assemble_mod:
    push "mod"
    jmp print_instr

assemble_store:
    push "store"
    jmp print_instr
assemble_retrieve:
    push "retrieve"
    jmp print_instr

assemble_label:
    push "label_"
    call print_string
    call read_unsigned
    printi
    push '\n'
    push ':'
    printc
    printc
    ret
assemble_call:
    push "call"
    jmp print_instr_unsigned
assemble_jmp:
    push "jmp"
    jmp print_instr_unsigned
assemble_jz:
    push "jz"
    jmp print_instr_unsigned
assemble_jn:
    push "jn"
    jmp print_instr_unsigned
assemble_ret:
    push "ret"
    jmp print_instr
assemble_end:
    push "end"
    jmp print_instr

assemble_printc:
    push "printc"
    jmp print_instr
assemble_printi:
    push "printi"
    jmp print_instr
assemble_readc:
    push "readc"
    jmp print_instr
assemble_readi:
    push "readi"
    jmp print_instr

read_char:
    drop
    push 0
    dup
    readc
    retrieve

    # Check for NUL and exit input
    dup
    jz .read_char_end
    ret
.read_char_end:
    end

read_integer:
    call read_char

    # case [Space]: positive integer
    dup
    push ' '
    sub
    jz read_unsigned

    # case [Tab]: negative integer
    dup
    push '\t'
    sub
    jz .read_integer_negative

    # case [LF]: zero
    dup
    push '\n'
    sub
    jz .read_integer_zero

    # default: ignore character
    jmp read_integer

.read_integer_negative:
    call read_unsigned
    push -1
    mul
    ret

.read_integer_zero:
    push 0
    ret

read_unsigned:
    push 0

.read_unsigned_loop:
    swap
    call read_char

    # case [Space]: insert 0 into integer
    dup
    push ' '
    sub
    jz .read_unsigned_0

    # case [Tab]: insert 1 into integer
    dup
    push '\t'
    sub
    jz .read_unsigned_1

    # case [LF]: terminate integer
    dup
    push '\n'
    sub
    jz .read_unsigned_end

    # default: ignore character
    jmp .read_unsigned_loop

.read_unsigned_0:
    swap
    push 2
    mul
    jmp .read_unsigned_loop

.read_unsigned_1:
    swap
    push 2
    mul
    push 1
    add
    jmp .read_unsigned_loop

.read_unsigned_end:
    swap
    ret

print_string:
    dup
    jz .print_string_end
    printc
    jmp print_string
.print_string_end:
    drop
    ret

print_instr:
    call .print_instr
    push "\n"
    printc
    ret

.print_instr:
    push "    "
    call print_string
    call print_string
    ret

print_instr_integer:
    call .print_instr
    push ' '
    printc
    call read_integer
    printi
    push '\n'
    printc
    ret

print_instr_unsigned:
    call .print_instr
    push ' '
    printc
    call read_unsigned
    printi
    push '\n'
    printc
    ret
